desc: SOX Band Filter
tags: filter
author: Dr. Thomas Tensi, 2019

//--------------------
// IMPORTS
//--------------------

import Base.jsfx-inc
import Base_memory.jsfx-inc
import Base_integerset.jsfx-inc
import Base_string.jsfx-inc
import Base_fixedarray.jsfx-inc
import Base_display.jsfx-inc
import Base_debug.jsfx-inc

import SOX__base.jsfx-inc
import SOX__samplequeue.jsfx-inc
import SOX__iirfilter.jsfx-inc
import SOX__biquadfilter.jsfx-inc

//--------------------
// SLIDERS
//--------------------
slider1:mode=0<0,1,1{No,Yes}>Unpitched Audio Mode?
slider2:frequency=1000.0<10.0,20000.0,1>Frequency [Hz]
slider3:bandwidth=1<0.001,20000,0.00001>Bandwidth
slider4:bandwidthKind=0<0,3,1{Frequency,Octaves,Q,Butterworth}>Bandwidth Kind

//--------------------
// INITIALIZATION
//--------------------
@init
    //============================================================

    function SOXBand_Graphics_handleKey (key)
    (
        Graphics_currentMode == Graphics_Mode_effect ? (
            Graphics_clear(Graphics_Color_black);
        );
    );

    //============================================================

//--------------------
// SLIDER CHANGE
//--------------------
@slider
    usesUnpitchedAudioMode = (mode == 1);

    // coefficients for biquad transform
    w0 = twoPi * frequency / srate;

    bandwidthAsFrequency =
        iif(bandwidthKind == SOXBandwidthKind_quality,
                frequency / bandwidth,
            bandwidthKind == SOXBandwidthKind_octaves,
                frequency * (2 ^ bandwidth - 1) * 2 ^ (-bandwidth / 2),
            bandwidth);

    a2 = exp(-twoPi * bandwidthAsFrequency / srate);
    a1 = -4 * a2 / (1 + a2) * cos(w0);
    a0 = 1;
    b2 = 0;
    b1 = 0;
    b0 = sqrt(1 - sqr(a1) / (4 * a2)) * (1 - a2);

    usesUnpitchedAudioMode ? (
        factor = sqrt((sqr(1 + a2) - sqr(a1)) * (1 - a2) / (1 + a2)) / b0;
        b0 *= factor;
    );

    SOXBiquadFilter_init(filter, b0, b1, b2, a0, a1, a2);
    Debug_clear();
    SOXBiquadFilter_print(filter);

//--------------------
// GRAPHICS
//--------------------
@gfx
    key = gfx_getchar();
    key = Graphics_MTSMatrix_handleKey(key);
    SOXBand_Graphics_handleKey(key);
