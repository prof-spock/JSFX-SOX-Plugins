desc: fixed array routines
// implements an array of fixed length where length is stored as first
// entry in array (for bounds checking and length information)
author: Dr. Thomas Tensi, 2019

//requires Base.jsfx-inc
//requires Base_memory.jsfx-inc

@init
    //========================================

    FixedArray_firstErrorCode = 999900;

    //--------------------

    function FixedArray_positionIsOkay (array, position)
        /** Checks whether <position> is in range of <array> */
    (
        0 < position && position <= array[0]
    );

    //--------------------

    function FixedArray_addressCHECKED (array, position)
        /** Gets address of element in <array> at <position> (with
            bounds checking) */
        local (result)
    (
        FixedArray_positionIsOkay(array, position) ? (
            result = ADDR(array, position);
        ) : (
            result = 1E9;
            signalError(FixedArray_firstErrorCode + 1);
        );

        result
    );

    //--------------------

    function FixedArray_addressFAST (array, position)
        /** Gets address of element in <array> at <position> (without
            bounds checking) */
    (
        array + position
    );

    //--------------------

    function FixedArray_getAtCHECKED (array, position)
        /** Gets element in <array> at <position> (with bounds checking) */
        local (result)
    (
        FixedArray_positionIsOkay(array, position) ? (
            result = array[position];
        ) : (
            result = 0;
            signalError(FixedArray_firstErrorCode + 2);
        );

        result
    );

    //--------------------

    function FixedArray_getAtFAST (array, position)
        /** Gets element in <array> at <position> (without bounds
            checking) */
    (
        array[position]
    );

    //--------------------

    function FixedArray_setAtCHECKED (array, position, value)
        /** Sets element in <array> at <position> to <value> (with
            bounds checking) */
    (
        FixedArray_positionIsOkay(array, position) ? (
            array[position] = value;
        ) : (
            signalError(FixedArray_firstErrorCode + 3);
        );
    );

    //--------------------

    function FixedArray_setAtFAST (array, position, value)
        /** Sets element in <array> at <position> to <value> (without
            bounds checking) */
    (
        array[position] = value;
    );

    //--------------------
    // EXPORTED ROUTINES
    //--------------------

    function FixedArray_make (length)
        /** Makes a fixed array with <length> elements */
        local (self)
    (
        // first entry contains the length, rest are the elements
        self = Memory_allocate(length + 1);
        self[0] = length;
        self
    );

    //--------------------

    function FixedArray_address (array, position)
        /** Gets address of element in <array> at <position> */
        local (result)
    (
        isCheckedMode ? (
            result = FixedArray_addressCHECKED(array, position);
        ) : (
            result = FixedArray_addressFAST(array, position);
        );

        result
    );

    //--------------------

    function FixedArray_clear (array)
        /** Clears <array> to zero entries */
    (
        Memory_set(array + 1, array[0], 0);
    );

    //--------------------

    function FixedArray_getAt (array, position)
        /** Gets element in <array> at <position> */
        local (result)
    (
        isCheckedMode ? (
            result = FixedArray_getAtCHECKED(array, position);
        ) : (
            result = FixedArray_getAtFAST(array, position);
        );

        result
    );

    //--------------------

    function FixedArray_length (array)
        /** Gets length of <array> */
    (
        array[0]
    );

    //--------------------

    function FixedArray_setAt (array, position, value)
        /** Sets element in <array> at <position> to <value> */
    (
        isCheckedMode ? (
            FixedArray_setAtCHECKED(array, position, value);
        ) : (
            FixedArray_setAtFAST(array, position, value);
        );
    );

    //========================================
