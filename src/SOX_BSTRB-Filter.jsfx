desc: SOX Bass/Treble Filter
tags: filter
author: Dr. Thomas Tensi, 2019

//--------------------
// IMPORTS
//--------------------

import Base.jsfx-inc
import Base_memory.jsfx-inc
import Base_integerset.jsfx-inc
import Base_string.jsfx-inc
import Base_fixedarray.jsfx-inc
import Base_display.jsfx-inc
import Base_debug.jsfx-inc

import SOX__base.jsfx-inc
import SOX__samplequeue.jsfx-inc
import SOX__iirfilter.jsfx-inc
import SOX__biquadfilter.jsfx-inc

//--------------------
// SLIDERS
//--------------------
slider1:mode=0<0,1,1{Bass,Treble}>Mode
slider2:dBFilterGain=0<-25,25,0.05>Gain [dB]
slider3:frequency=1000.0<10.0,20000.0,1>Frequency [Hz]
slider4:bandwidth=1<0.001,20000,0.00001>Bandwidth
slider5:bandwidthKind=0<0,4,1{Frequency,Octaves,Q,Butterworth,Slope}>Bandwidth Kind

//--------------------
// INITIALIZATION
//--------------------
@init
    //============================================================

    function SOXBsTrb_Graphics_handleKey (key)
    (
        Graphics_currentMode == Graphics_Mode_effect ? (
            Graphics_clear(Graphics_Color_black);
        );
    );

    //============================================================

    SOXBsTrb_bassFilter = 0;

//--------------------
// SLIDER CHANGE
//--------------------
@slider
    a = 10^(dBFilterGain/40);

    // coefficients for biquad transform
    w0 = twoPi * frequency / srate;
    alpha =
       SOXBiquadFilter_alphaForBandwidth(bandwidth, bandwidthKind,
                                         frequency, dBFilterGain);

    mode == SOXBsTrb_bassFilter ? (
        // bass filter => low shelf
        b0 =      a * ( (a+1) - (a-1)*cos(w0) + 2*sqrt(a)*alpha );
        b1 =  2 * a * ( (a-1) - (a+1)*cos(w0)                   );
        b2 =      a * ( (a+1) - (a-1)*cos(w0) - 2*sqrt(a)*alpha );
        a0 =            (a+1) + (a-1)*cos(w0) + 2*sqrt(a)*alpha;
        a1 = -2 *     ( (a-1) + (a+1)*cos(w0)                   );
        a2 =            (a+1) + (a-1)*cos(w0) - 2*sqrt(a)*alpha;
    ) : (
        // treble filter => high shelf
        b0 =      a * ( (a+1) + (a-1)*cos(w0) + 2*sqrt(a)*alpha );
        b1 = -2 * a * ( (a-1) + (a+1)*cos(w0)                   );
        b2 =      a * ( (a+1) + (a-1)*cos(w0) - 2*sqrt(a)*alpha );
        a0 =            (a+1) - (a-1)*cos(w0) + 2*sqrt(a)*alpha;
        a1 =  2 *     ( (a-1) - (a+1)*cos(w0)                   );
        a2 =            (a+1) - (a-1)*cos(w0) - 2*sqrt(a)*alpha;
    );

    SOXBiquadFilter_init(filter, b0, b1, b2, a0, a1, a2);

//--------------------
// GRAPHICS
//--------------------
@gfx
    key = gfx_getchar();
    key = Graphics_MTSMatrix_handleKey(key);
    SOXBsTrb_Graphics_handleKey(key);
